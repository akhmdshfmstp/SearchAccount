apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode version_code
        versionName version_name
        multiDexEnabled = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "API_ENV"
    productFlavors {
        dev {
            dimension "API_ENV"
            versionNameSuffix "-dev"
            buildConfigField "String", "BUILD_VARIANT", '"dev"'

            buildConfigField("String", "API_BASE_URL", '"https://api.github.com/"')

            applicationId "com.test.github.search.user.akhmadshofimustopo.dev"
            resValue "string", "app_name", "Akhmad Shofi Mustopo-DEV"
            resValue "string", "package_name", "com.test.github.search.user.akhmadshofimustopo.dev"
        }
        production {
            dimension "API_ENV"
            buildConfigField "String", "BUILD_VARIANT", '"prod"'

            buildConfigField("String", "API_BASE_URL", '"https://api.github.com/"')

            applicationId "com.test.github.search.user.akhmadshofimustopo"
            resValue "string", "app_name", "Akhmad Shofi Mustopo"
            resValue "string", "package_name", "com.test.github.search.user.akhmadshofimustopo"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false

            buildConfigField "boolean", "RELEASE", "false"
        }
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs false

            proguardFile '/proguard/proguard-retrofit2.pro'
            proguardFile '/proguard/proguard-constraint-layout.pro'
            proguardFile '/proguard/proguard-okhttp3.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '/proguard/proguard-constraint-layout.pro'

            buildConfigField "boolean", "RELEASE", "true"
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name == 'release') {
            variant.getFlavors().each { flavor ->
                if (flavor.dimension == 'API_ENV' && flavor.name == 'dev') {
                    variant.setIgnore(true)
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
        ignoreWarnings true
        quiet true
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation(project(":framework"))
    implementation(project(":datamodule"))

    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'

    debugImplementation "com.ashokvarma.android:gander-imdb:$gander_version"
    releaseImplementation "com.ashokvarma.android:gander-no-op:$gander_version"

    /* android test */
    testImplementation "junit:junit:4.13"
    testImplementation "androidx.test:core:1.3.0"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation "org.hamcrest:hamcrest:2.2"
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:2.2.0"
}
